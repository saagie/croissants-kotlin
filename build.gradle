buildscript {
	ext {
		kotlinVersion = '1.2.20'
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.19.2")
        classpath 'ru.vyarus:gradle-mkdocs-plugin:1.0.0'
	}
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: "com.palantir.docker"
apply plugin: 'ru.vyarus.mkdocs'

group = 'io.saagie'
version = "1.1.$System.env.TRAVIS_BUILD_NUMBER"
sourceCompatibility = 1.8
compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

repositories {
    jcenter()
}


ext.jacksonModuleVersion="2.9.4.1"
ext.jbotVersion="3.0.2"
ext.swaggerVersion="2.8.0"
ext.kluentVersion="1.35"
ext.mockitoVersion="1.5.0"

dependencies {
	compile('org.springframework.boot:spring-boot-starter')
	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.security.oauth:spring-security-oauth2")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonModuleVersion}")
    compile("me.ramswaroop.jbot:jbot:${jbotVersion}")
    compile("io.springfox:springfox-swagger2:${swaggerVersion}")
    compile("io.springfox:springfox-swagger-ui:${swaggerVersion}")
    //Just for the login/logout demo START
    compile("org.webjars:angularjs:1.4.3")
    compile("org.webjars:jquery:2.1.1")
    compile("org.webjars:bootstrap:3.2.0")
    compile("org.webjars:webjars-locator")

	testCompile("org.springframework.boot:spring-boot-starter-test")
	testCompile("org.springframework.security:spring-security-test")
	testCompile("org.amshove.kluent:kluent:${kluentVersion}")
	testCompile("com.nhaarman:mockito-kotlin-kt1.1:${mockitoVersion}")
	testCompile("org.jetbrains.kotlin:kotlin-test:${kotlinVersion}")
}

docker {
    name "saagie/${jar.baseName}:${version}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

mkdocs {
    sourcesDir = 'src/main/guide'
    buildDir= 'src/main/resources/static/guide'
}